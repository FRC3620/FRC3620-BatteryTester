plugins {
    id 'java'
    id 'org.hidetake.ssh' version '2.11.2'
}

//group 'org.usfirst.frc3620.battery-tester'
//version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs 'src'
            srcDirs('pi4j-example-components/src/main/java')
        }
    }
}

ext {
    log4jVersion = '2.20.0'
    pi4jVersion = '2.3.0'
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    implementation 'io.undertow:undertow-websockets-jsr:2.3.5.Final'

    implementation 'org.slf4j:slf4j-api:2.0.7'

    implementation "org.apache.logging.log4j:log4j-core:$log4jVersion"
    testImplementation "org.apache.logging.log4j:log4j-slf4j2-impl:$log4jVersion"

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.0'

    implementation "com.pi4j:pi4j-plugin-raspberrypi:$pi4jVersion"
    implementation "com.pi4j:pi4j-plugin-pigpio:$pi4jVersion"
    implementation "com.pi4j:pi4j-plugin-linuxfs:$pi4jVersion"
}

test {
    useJUnitPlatform()
}

remotes {
    teststand {
        host = 'mqtt.local'
        user = 'pi'
        knownHosts = allowAnyHosts
        identity = file('ssh/private')
    }
}

task deploy {
    doLast {
        ssh.run {
            session(remotes.teststand) {
                put from: "$buildDir/libs/FRC3620-BatteryTester.jar", into: './'
                execute 'ls'
            }
        }
    }
}

// https://www.baeldung.com/gradle-source-sets
task printSourceSetInformation(){

    doLast{
        sourceSets.each { srcSet ->
            println "["+srcSet.name+"]"
            print "-->Source directories: "+srcSet.allJava.srcDirs+"\n"
            print "-->Output directories: "+srcSet.output.classesDirs.files+"\n"
            print "-->Compile classpath:\n"
            srcSet.compileClasspath.files.each {
                print "  "+it.path+"\n"
            }
            println ""
        }
    }
}