plugins {
    id 'java'
    id 'org.hidetake.ssh' version '2.11.2'
}

//group 'org.usfirst.frc3620.battery-tester'
//version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    log4jVersion = '2.20.0'
    pi4jVersion = '2.3.0'
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    implementation 'io.undertow:undertow-websockets-jsr:2.3.5.Final'

    implementation 'org.slf4j:slf4j-api:2.0.7'

    implementation "org.apache.logging.log4j:log4j-core:$log4jVersion"
    implementation "org.apache.logging.log4j:log4j-slf4j2-impl:$log4jVersion"

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.0'

    implementation 'com.diozero:diozero-core:1.3.5'

    implementation 'com.google.zxing:javase:3.5.1'

    implementation 'com.github.sarxos:webcam-capture:0.3.12'

}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes "Main-Class": "org.usfirst.frc3620.batterytester.Application"
        // attributes "Main-Class": "org.usfirst.frc3620.batterytester.SN3218"
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

remotes {
    tester {
        host = 'battery-tester.local'
        user = 'pi'
        knownHosts = allowAnyHosts
        identity = file('ssh/private')
    }
}

task deploy {
    doLast {
        ssh.run {
            session(remotes.tester) {
                put from: "$buildDir/libs/FRC3620-BatteryTester.jar", into: './'
                execute 'ls'
            }
        }
    }
}

deploy.dependsOn(build)

configure(deploy) {
    group = 'build'
    description = 'Deploy jar to remote system'
}